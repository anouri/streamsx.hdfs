

composite Reduce(input InStream ; output WordCount)
{
	graph
		stream<InStream> Puncted = Custom(InStream)
		{
			logic
				onTuple InStream : submit(InStream, Puncted) ;
				onPunct InStream :
				{
					if(currentPunct() == Sys.FinalMarker)
					{
						submit(Sys.WindowMarker, Puncted) ;
						submit(Sys.FinalMarker, Puncted) ;
					}

				}

		}

		stream<rstring word, int32 count> WordCount = Aggregate(Puncted as I)
		{
			window
				I : tumbling, punct(), partitioned ;
			param
				partitionBy : word ;
			output
				WordCount : count = Count() ;
		}

		() as sink = FileSink(WordCount)
		{
			param
				file : "words" +(rstring) getChannel() + ".txt" ;
		}

}

composite WordCount
{
	param
		expression<int32> $numMappers :(int32) getSubmissionTimeValue("numMappers",
			"1") ;
		expression<int32> $numReducers :(int32) getSubmissionTimeValue("numReducers",
			"1") ;
	graph
		@parallel(width = $numMappers)
		stream<rstring word> WordStream = ReadAndTokenize()
		{
			param
				inputFile : getSubmissionTimeValue("inputFile", "allEnron.txt") ;
			config
				placement : partitionColocation("map") ;
		}

		@parallel(width = $numReducers, partitionBy = [ { port = WordStream,
			attributes = [ word ] } ])
		stream<rstring word, int32 count> WordCount = Reduce(WordStream)
		{
			config
				placement : partitionColocation("reduce") ;
		}

}

